plugins {
    id "java"
    id "application"
}

def timefoldVersion = "1.28.0"
def logbackVersion = "1.5.20"
def junitVersion = "6.0.1"
def assertjVersion = "3.27.6"
def profile = System.properties['profile'] ?: ''

group = "ch.kleemans"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation platform("ai.timefold.solver:timefold-solver-bom:${timefoldVersion}")
    implementation "ai.timefold.solver:timefold-solver-core"
    runtimeOnly "ch.qos.logback:logback-classic:${logbackVersion}"
    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")

    // Testing
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "ai.timefold.solver:timefold-solver-test"

    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs << "-parameters"
}

compileTestJava {
    options.encoding = "UTF-8"
}

application {
    mainClass = "ch.kleemans.ludoplanung.LudoApp"
}

test {
    // Log the test execution results.
    testLogging {
        events "passed", "skipped", "failed"
    }

    if (profile == 'slowly') {
        useJUnitPlatform()
    } else {
        useJUnitPlatform {
            excludeTags "slowly"
        }
    }
}
